REM  *****  BASIC  *****
Option VBASupport 1

Sub Main
	' マクロの実行に伴う警告の表示を無効化
	Application.DisplayAlerts = False
	
	' 出張者リストExcelファイル（WB）のパス
	' B1セルに記述されていることを期待している
	dim bussinessTravelersFilePath as String
	bussinessTravelersFilePath = ThisWorkBook.WorkSheets(1).Cells(1, 2)
	
	' 出張者リストのWBオブジェクト
	dim bussinessTravelersWb as Workbook
	Set bussinessTravelersWb = setWorkbook(bussinessTravelersFilePath)

	' 食堂利用者リストWBのパス
	dim cafeteriaUsersFilePath as String
	cafeteriaUsersFilePath = ThisWorkBook.WorkSheets(1).Cells(2, 2)
	
	' 食堂利用者リストのWBオブジェクト
	dim cafeteriaUsersWb as Workbook
	Set cafeteriaUsersWb = setWorkbook(cafeteriaUsersFilePath)
	
	' 出張者の名前を格納する配列
	' 本来配列内で定義するべきだが、VBAの特性上変数がすべて事前定義されるためここに記述
	dim travelerNames(0) as String
	
	' 各日付のループ
	' 月曜～土曜までの6回
	' 食堂利用者リストの値をループ取得する
	dim i as Integer
	For i = 0 To 5
		' 日付が格納されている行番号
		' 5: 最初に取得する行番号
		' 6: 次に読み込む行番号までの幅
		dim rowCount as Integer
		rowCount = 5 + (6 * i)
		
		' 日付の取得
		dim targetDay as Date
		targetDay = cafeteriaUsersWb.Worksheets(1).Cells(rowCount, 1).value
		
		' 名前リストの初期化
		Redim travelerNames(0)
		
		' 各部屋のループ
		' 201～208までの8回
		' 取得した日付の各部屋の利用者をループ取得する
		dim j as Integer
		For j = 0 To 7
			' 行番号の指定
			' 4: 日付が開始されるまでの不要な行数
			dim targetRowNumber as Integer
			targetRowNumber = 4 + Day(targetDay)
			
			' 列番号の指定
			' 3: 最初に取得する列番号
			' 5: 次に取得する列番号までの距離
			dim targetColumnNumber as Integer
			targetColumnNumber = 3 + (5 * j)
			
			' 参照したセルの値
			dim cellValue as String
			cellValue = bussinessTravelersWb.Worksheets(1).Cells(targetRowNumber, targetColumnNumber).value

			' 値が空の場合は処理をスキップ
			IF cellValue = "" Then
				GoTo NextJ
    		End IF
    		
    		' 半角または全角の空白までの文字列（基本苗字）を抽出
    		cellValue = Split(cellValue, " ")(0)
			cellValue = Split(cellValue, "　")(0)
    		
    		' 配列を1大きいサイズに再定義
			Redim Preserve travelerNames(UBound(travelerNames) + 1)
			
			' 取得した値を格納
			travelerNames(UBound(travelerNames) - 1) = cellValue
			
		NextJ:
		Next j
		
		' 出張者がいない日の場合は以下の処理をスキップし次の日の処理へ
		IF UBound(travelerNames) = 0 Then
			GoTo NextI
		End IF
		
		' その日付の名前一覧のループ
		dim k as Integer
		For k = 0 To UBound(travelerNames) - 1
			dim travelerName as String
			travelerName = travelerNames(k)
			
			' 朝・昼・夜のループ
			dim l as Integer
			For l = 0 To 2
				' 名前を書き込む行番号の指定
				' 4      : 最初に入力する行番号までの不要なヘッダ行数
				' 6      : 次の日付までの行数
				' k Mod 6: kを6で割ったあまり、7個目で0に戻る
				dim insertRowNumber as Integer
				insertRowNumber = 4 + (6 * i) + (k Mod 6)
				
				' 名前を書き込む列番号の指定
				dim insertColumnNumber as Integer
				insertColumnNumber = 2 + (3 * l) + (k \ 6)
								
				' 指定されたセルに名前を入力
				cafeteriaUsersWb.Worksheets(1).Cells(insertRowNumber, insertColumnNumber).value = travelerName
			Next l
		Next k
	NextI:
	Next i
End Sub

' WBの取得
' Args
' 	- filePath: 読み込むWBのパス
' Return
'	WBのオブジェクト
Function setWorkbook(filePath as String) as Workbook
	' `filePath` に定義されたパスにファイルの存在確認
    If Dir(filePath) <> "" Then
    	' 存在する場合はファイルを開く
    	' -> 後述で `ActiveWorkbook` として読み込むため
        Workbooks.Open filePath, ReadOnly := True
    Else
    	' 存在しない場合はダイアログを表示し終了
        MsgBox "ファイルが存在しません。ファイルパスを確認してください。"&filePath
        Exit Function
    End If

    setWorkbook = ActiveWorkbook
End Function
